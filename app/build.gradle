apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'spoon'
apply from: '../jacoco.gradle'

def appId = "com.makingiants.android.banjotuner"
def appVersionName = "1.5.2"
def appVersionCode = 111

android {
  compileSdkVersion setup.targetSdk
  buildToolsVersion setup.buildTools

  defaultConfig {
    applicationId appId
    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk
    versionCode appVersionCode
    versionName appVersionName

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    // Enable just English configs for dependencies, allowing to shrink apk size more
    resConfigs "en", "pt"

    resValue "string", "ads_unit_id_banner", "$BANJEN_ADS_UNIT_ID_BANNER"
  }

  buildTypes {
    debug {
      minifyEnabled false
      useProguard false // For performance
      testCoverageEnabled = true

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      useProguard true

      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    packagingOptions {
      exclude 'META-INF/services/org.xmlpull.v1.XmlPullParserFactory'
    }
  }

  signingConfigs {
    release {
      if (project.hasProperty('BANJEN_SIGN_PATH')) {
        storeFile file(project.property("BANJEN_SIGN_PATH"))
        storePassword project.property("BANJEN_SIGN_PWD")
        keyAlias project.property("BANJEN_SIGN_ALIAS")
        keyPassword project.property("BANJEN_SIGN_PWD")
      }
    }
  }
}

def excludeAnnotations = {
  exclude group: 'com.android.support', module: 'support-annotations'
}

dependencies {
  compile "com.android.support:appcompat-v7:${deps.supportLib}"
  compile "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"

  // Ads/Crash
  compile 'com.crashlytics.sdk.android:crashlytics:2.9.1'
  compile "com.google.firebase:firebase-core:${deps.firebase}"
  compile "com.google.firebase:firebase-ads:${deps.firebase}"

  testCompile 'junit:junit:4.12'
  testCompile "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$deps.kotlin"

  androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.2', excludeAnnotations
  androidTestCompile 'com.android.support.test:runner:1.0.2', excludeAnnotations
  androidTestCompile 'com.android.support.test:rules:1.0.2', excludeAnnotations
  androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
  androidTestCompile 'com.jraska:falcon-spoon-compat:1.0.4', {
    exclude group: 'com.squareup.spoon', module: 'spoon-client'
  }
}

jacoco {
  toolVersion = deps.jacoco
}

spoon {
  debug = true
  codeCoverage = true
  grantAllPermissions = true
}


apply plugin: 'com.google.gms.google-services'